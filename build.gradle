buildscript {
	repositories {
		jcenter()

		maven {
			url "http://files.minecraftforge.net/maven"
		}
	}

	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"

version = "1.12.2-2.1.1.0"
group = "com.therandomlabs.randomtweaks"
archivesBaseName = "randomtweaks"

sourceCompatibility = targetCompatibility = "1.8"

compileJava {
	sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
	version = "1.12.2-latest"
	runDir = "run"
	mappings = "snapshot_20180705"

	replace "@VERSION@", project.version
	replace "@FINGERPRINT@", project.findProperty("signSHA1")
}

repositories {
	maven {
		url "http://www.ryanliptak.com/maven/"
	}

	maven {
		url "https://jitpack.io"
	}

	maven {
		url "https://maven.mcmoddev.com"
	}

	maven {
		url "http://dvs1.progwml6.com/files/maven"
	}

	maven {
		url "http://tehnut.info/maven"
	}

	maven {
		url "https://maven.blamejared.com"
	}
}

dependencies {
	deobfCompile "applecore:AppleCore:1.12.2-3.1.1:api"
	deobfCompile "com.github.Buuz135:Together-Forever:master-SNAPSHOT"
}

processResources {
	//Ensure this task is redone when the versions change
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	//Replace version and mcversion in mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
}

jar {
	manifest {
		attributes "FMLAT": "randomtweaks_at.cfg"
	}
}

task signJar(type: SignJar, dependsOn: reobfJar) {
	onlyIf {
		project.hasProperty("keyStore")
	}

	keyStore = project.findProperty("keyStore")
	alias = project.findProperty("keyStoreAlias")
	storePass = project.findProperty("keyStorePass")
	keyPass = project.findProperty("keyStoreKeyPass")
	inputFile = jar.archivePath
	outputFile = jar.archivePath
}

build.dependsOn signJar
